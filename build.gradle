plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '6.0+'
    id "me.shedaniel.unified-publishing" version "0.1.+"
}

version = rootProject.version
group = rootProject.group // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "siege-machine-compat-${rootProject.minecraft_version}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'official', version: '1.20.1'

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'examplemod'
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'examplemod'
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'examplemod'
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'examplemod', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven { url "https://api.modrinth.com/maven" }
    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex("software\\.bernie.*")
            includeGroup("com.eliotlash.mclib")
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    implementation fg.deobf("maven.modrinth:medieval-siege-machines:${siege_machines_version}-forge")
    implementation fg.deobf("maven.modrinth:villager-recruits:${villager_recruits_version}-forge")
    implementation fg.deobf("software.bernie.geckolib:geckolib-forge-${minecraft_version}:${geckolib_version}")
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand (
                'version'                      : version,
                'group'                        : group,
                'minecraft_version'            : minecraft_version,
                'minecraft_min_version'        : minecraft_min_version,
                'minecraft_max_version'        : minecraft_max_version,
                'mod_name'                     : mod_name,
                'mod_title'                    : mod_title,
                'mod_author'                   : mod_author,
                'mod_id'                       : mod_id,
                'license'                      : license,
                'description'                  : description,
                'mod_description'              : description,
                'forge_version_range'          : forge_version_range,
                'credits'                      : credits,
                'villager_recruits_version'    : villager_recruits_version,
                'siege_machines_version'       : siege_machines_version
        )
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "examplemod",
                "Specification-Vendor"    : "examplemodsareus",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "examplemodsareus",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

unifiedPublishing {
    project {
        displayName = "[${rootProject.minecraft_version}] ${rootProject.mod_display_name} ${rootProject.version}"
        version = rootProject.version
        changelog = String.join("\n", rootProject.file("CHANGELOG.md").readLines())
        releaseType = "release"
        gameVersions = [rootProject.minecraft_version]
        gameLoaders = ["forge"]

        mainPublication tasks.jar

        relations {
            depends {
                curseforge = "geckolib"
            }
            includes {}
            optional {}
            conflicts {}
        }

        curseforge {
            token = System.getenv("CURSEFORGE_TOKEN") ?: ""
            id = rootProject.mod_curseforge_id

            displayName = "[${rootProject.minecraft_version}] ${rootProject.mod_display_name} ${rootProject.version}"
            version = rootProject.version
            changelog = String.join("\n", rootProject.file("CHANGELOG.md").readLines())
            releaseType = "release"
            gameVersions = [rootProject.minecraft_version]
            gameLoaders = ["forge"]

            mainPublication tasks.jar

            relations {
                depends "medieval-siege-machines"
                depends "recruits"
            }
        }
    }
}